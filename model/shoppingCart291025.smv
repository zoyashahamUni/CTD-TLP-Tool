MODULE main

VAR
  --Abstract system state
  mode  : {LoggedOut, Shopping, CheckedOut};           
  items : 0..5;
  step  : {login, logout, add, remove, checkout, idle}; --The last step that was done
  
  --Helper state variables not part of the formal states  
  logout_after_add :  boolean;    -- becomes true if logout happens after adding
  removed_ever:       boolean;    -- have we seen any Remove so far?
  max_items : 0..5;

ASSIGN
  init(mode)              := LoggedOut;
  init(items)             := 0;
  init(step)              := login; 
  init(logout_after_add)  := FALSE;
  init(removed_ever)      := FALSE;
  init(max_items)         := 0;
    
  --Coupling 'step' to mode
  next(mode) := 
    case
      mode = CheckedOut & step = idle             : CheckedOut; --The tests end here.
      mode = LoggedOut  & step = login            : Shopping;
      mode = Shopping   & (step in {add, remove}) : Shopping;
      mode = Shopping   & step=checkout & items >0: CheckedOut;    
      mode = Shopping   & step = logout           : LoggedOut; --In case there are items in the cart the items will be 0. If we want to make an infeasible condition we can mark here & items=0
    TRUE                                          : mode;
  esac;

  next(items) := 
    case
      -- reset cart on checkout & logout
      step in {logout, checkout}                  :0;
      mode = Shopping & step = add    & items<5   :items + 1;
      mode = Shopping & step = remove & items>0   :items - 1;
      TRUE                                        : items;
    esac;

  next(step) := 
    case
      next(mode) = LoggedOut    : {login};
      next(mode) = Shopping     : {add, remove, checkout, logout};
      next(mode) = CheckedOut   : {idle};
    esac;

  next(logout_after_add)  :=
    case
      step = logout & items > 0               : TRUE;
      TRUE                                        : logout_after_add;
    esac;

  next(removed_ever)                              := removed_ever | (step = remove);

  next(max_items) :=
    case
      next(items) > max_items                     : next(items);
      TRUE                                        : max_items;
    esac;

INVAR !(step = checkout & !(mode = Shopping & items > 0))     --checkout only while shopping and with items in the cart (1 at least)    
INVAR (step = logout -> items = 0)


DEFINE
  -- step detectors (ST)
  do_login                := (step = login);
  do_logout               := (step = logout);
  do_checkout             := (step = checkout);
  do_add                  := (step = add);
  do_remove               := (step = remove);

  -- Temporal properties (a, b, c)
  a_no_logout_after_add   := !logout_after_add;     -- A
  b_target                := 3;                     --can be also 4 or 5
  b_max_items             := (max_items = b_target);-- B Boolean true if the max reached the target
  c_no_remove             := !removed_ever;         -- C